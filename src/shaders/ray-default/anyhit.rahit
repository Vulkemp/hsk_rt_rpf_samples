#version 460
#extension GL_KHR_vulkan_glsl : enable // Vulkan-specific syntax
#extension GL_GOOGLE_include_directive : enable // Include files
#extension GL_EXT_ray_tracing : enable // Raytracing
#extension GL_EXT_nonuniform_qualifier : enable // Required for asserting that some array indexing is done with non-uniform indices

// Include structs and bindings

#include "../../../hsk_rt_rpf/src/shaders/rt_common/bindpoints.glsl" // Bindpoints (= descriptor set layout)
#include "../../../hsk_rt_rpf/src/shaders/rt_common/tlas.glsl"
#include "../../../hsk_rt_rpf/src/shaders/common/materialbuffer.glsl" // Material buffer for material information and texture array
#include "../../../hsk_rt_rpf/src/shaders/rt_common/geometrymetabuffer.glsl" // GeometryMeta information
#include "../../../hsk_rt_rpf/src/shaders/rt_common/geobuffers.glsl" // Vertex and index buffer aswell as accessor methods
#include "../../../hsk_rt_rpf/src/shaders/common/normaltbn.glsl" // Normal calculation in tangent space
#include "../../../hsk_rt_rpf/src/shaders/pbr/colorspace.glsl"
#include "../../../hsk_rt_rpf/src/shaders/pbr/specularbrdf.glsl"

// Declare hitpayloads

#define HITPAYLOAD_IN // Declare hitpayload returned to the ray generation source resulting in the invocation of this closesthit shader instance
#define HITPAYLOAD_OUT // Declare hitpayload passed to hit/miss shaders of any child shaders
#include "../../../hsk_rt_rpf/src/shaders/rt_common/payload.glsl"

hitAttributeEXT vec2 attribs; // Barycentric coordinates

void main()
{
	if (ReturnPayload.Attenuation <= 0.001f)
	{
		return;
	}

	// Calculate barycentric coords from hitAttribute values
	const vec3 barycentricCoords = vec3(1.0f - attribs.x - attribs.y, attribs.x, attribs.y);
	
	// Get geometry meta info
	GeometryMeta geometa = GetGeometryMeta(uint(gl_InstanceCustomIndexEXT), uint(gl_GeometryIndexEXT));

	// get primitive indices
	const uvec3 indices = GetIndices(geometa, uint(gl_PrimitiveID));

	// get primitive vertices
	Vertex v0, v1, v2;
	GetVertices(indices, v0, v1, v2);

	// calculate uv
    const vec2 uv = v0.Uv * barycentricCoords.x + v1.Uv * barycentricCoords.y + v2.Uv * barycentricCoords.z;

	// get material 
	MaterialBufferObject material = GetMaterialOrFallback(geometa.MaterialIndex);
	MaterialProbe probe = ProbeMaterial(material, uv);

    if (probe.BaseColor.a == 0.f)
    {
        ignoreIntersectionEXT;
    }
}
